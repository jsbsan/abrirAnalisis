' Gambas class file

Public BooleanEncontrada As Boolean = False

Public Sub Form_Open()

    If ModuleMeEjecuto.MeEjecuto() Then
        'fin de la aplicaciÃ³n...
        Me.Close()
        Return
    Endif

    Settings.read(Me)
    Me.title = "Buscar ficheros De Analisis. Version " & Application.Version

    If Settings["rutaDirectorio"] = "" Then Settings["rutaDirectorio"] = "/home/hdd/home/mint/analisis de apuestas"

End

Public Sub ButtonAbrir_Click()

    Dim TextoBusqueda As String
    Dim rutaBusqueda As String
    Dim arrayfichero As String[]
    Dim nombrefile As String
    Dim encontrada As New String[]

    rutabusqueda = Settings["rutaDirectorio", "/home/hdd/home/mint/analisis de apuestas"]

    If TextBox1.text = "" Then
        TextoBusqueda = ButtonDecenaMillar.text & ButtonUnidadMillar.text & " " & ButtonCenetena.text & ButtonDecena.text & ButtonUnidad.Text
    Else
        If Len(TextBox1.Text) = 5 Then
            TextoBusqueda = Mid$(TextBox1.Text, 1, 2) & " " & Mid$(TextBox1.Text, 3, 5)
        Else
            If InStr(TextBox1.text, "-") = 0 Then
                TextoBusqueda = Mid$(TextBox1.Text, 1, 6) 'limito la busqueda hasta el sexto caracter
            Else
                TextoBusqueda = Mid$(TextBox1.Text, 1, InStr(TextBox1.text, "-") - 1) 'limito la busqueda hasta el "-" en el nombre
                If Len(TextoBusqueda) = 5 Then
                    TextoBusqueda = Mid$(TextoBusqueda, 1, 2) & " " & Mid$(TextoBusqueda, 3, 5)
                Endif
            Endif

        Endif

    Endif

    arrayfichero = Dir(rutaBusqueda, "*" & TextoBusqueda & "*", gb.File)
    For Each nombrefile In arrayfichero
        encontrada.add(nombrefile)

    Next

    BooleanEncontrada = False

    For Each nombrefile In encontrada
        If InStr(nombrefile, "lock") = 0 Then
            'lo puedo abrir
            Desktop.Open(rutabusqueda & "/" & nombrefile)
            BooleanEncontrada = True
            Break ''solo abrir el primero
        Endif
    Next

    If BooleanEncontrada = False Then
        'mostrar ventana de no encuentro nada..
        FormNoEncuentroNada.Show()
        TextBox1.SetFocus()
        TextBox1.Pos = 0
    Endif

End

Public Sub TextBox1_KeyPress()

    If Key.code = Key.enter Or Key.code = Key.Return Then
        ButtonAbrir_Click()
    Endif

End

Public Sub Form_Close()

    Settings.Write(Me)

End

Public Sub cifras_MouseUp()
    'hacer click izquierdo aumenta un valor hasta llegar al 9 al siguiente cambia a 0

    If Mouse.left Then
        If Val(Last.text) > 8 Then
            Last.text = "0"
        Else

            Last.text = Str$(Val(Last.text) + 1)
        Endif
    Endif

    'hacer click derecho disminuye el valor hasta llegar al 0 al siguiente pasa al 9
    If Mouse.Right Then
        If Val(Last.text) > 0 Then
            Last.text = Str$(Val(Last.text) - 1)
        Else
            Last.text = Str$(9)

        Endif
    Endif

End

Public Sub ButtonLimpiar_Click()

    TextBox1.text = ""

End

Public Sub Buttonpegar_Click()

    Print Clipboard.Paste("text/html")
    Print Clipboard.Paste("text/plain;charset=utf-8")
    TextBox1.text = Replace(Clipboard.Paste("text/plain;charset=utf-8"), "\n", "")

End

Public Sub ButtonAbrirConfiguracion_Click()

    Exec ["gedit", User.Home & "/.config/gambas3/abrirAnalisis.conf"]

End
